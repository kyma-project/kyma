#!/usr/bin/env groovy
def label = "kyma-${UUID.randomUUID().toString()}"
def application = "event-bus"
def isMaster = params.GIT_BRANCH == 'master'

def dockerPushRoot = isMaster
    ? "${env.DOCKER_REGISTRY}"
    : "${env.DOCKER_REGISTRY}snapshot/"

def dockerImageTag = isMaster
    ? params.APP_VERSION
    : params.GIT_BRANCH

def imageNameTester = 'event-bus-e2e-tester'
def imageNameSubscriber = 'event-bus-e2e-subscriber'

echo """
********************************
Job started with the following parameters:
DOCKER_REGISTRY=${env.DOCKER_REGISTRY}
DOCKER_CREDENTIALS=${env.DOCKER_CREDENTIALS}
GIT_REVISION=${params.GIT_REVISION}
GIT_BRANCH=${params.GIT_BRANCH}
APP_VERSION=${params.APP_VERSION}
APP_FOLDER=${env.APP_FOLDER}
dockerPushRoot=${dockerPushRoot}
dockerImageTag=${dockerImageTag}
********************************
"""

podTemplate(label: label) {
    node(label) {
        try {
            timestamps {
                timeout(time:20, unit:"MINUTES") {
                    ansiColor('xterm') {
                        stage("setup") {
                            checkout scm

                            if(dockerImageTag == "") {
                                error("No version for docker tag defined, please set APP_VERSION parameter for master branch or GIT_BRANCH parameter for any branch")
                            }

                            withCredentials([usernamePassword(credentialsId: env.DOCKER_CREDENTIALS, passwordVariable: 'pwd', usernameVariable: 'uname')]) {
                                sh "docker login -u $uname -p '$pwd' $env.DOCKER_REGISTRY"
                            }
                        }

                        stage("install dependencies $application") {
                            execute("/", "make resolve")
                        }

                        stage("code quality") {
                            execute("/", "gometalinter --skip=generated --vendor --deadline=2m --disable-all "
                            + "--enable=vet "
                            + "./...")
                        }

                        stage("build and test - event-bus-e2e-tester") {
                            execute("/e2e-tester/", "make clean build")
                        }

                        stage("build image - event-bus-e2e-tester") {
                            dir(env.APP_FOLDER + '/e2e-tester/') {
                                sh "./dockerBuild.sh event-bus-e2e-tester latest"
                            }
                        }

                        stage("build and test - event-bus-e2e-subscriber") {
                            execute("/e2e-subscriber/", "make clean build")
                        }

                        stage("build image - event-bus-e2e-subscriber") {
                            dir(env.APP_FOLDER + '/e2e-subscriber/') {
                                sh "./dockerBuild.sh event-bus-e2e-subscriber latest"
                            }
                        }

                        if (isMaster) {
                            stage("IP scan $application (Sourceclear)"){
                                withCredentials([string(credentialsId: 'SRCCLR_API_TOKEN', variable: 'SRCCLR_API_TOKEN')]) {
                                    execute("/", "make scan", "SRCCLR_API_TOKEN=$SRCCLR_API_TOKEN")
                                }
                            }
                        }

                        stage("push image - event-bus-e2e-tester") {
                            def imageName = "${dockerPushRoot}${imageNameTester}:${dockerImageTag}"
                            sh "docker tag event-bus-e2e-tester:latest ${imageName}"
                            sh "docker push ${imageName}"
                            echo "pushed image event-bus-e2e-tester: ${imageName}"
                        }

                        stage("push image - event-bus-e2e-subscriber") {
                            def imageName = "${dockerPushRoot}${imageNameSubscriber}:${dockerImageTag}"
                            sh "docker tag event-bus-e2e-subscriber:latest ${imageName}"
                            sh "docker push ${imageName}"
                            echo "pushed image event-bus-e2e-subscriber: ${imageName}"
                        }

                    }
                }
            }
        } catch (ex) {
            echo "Got exception: ${ex}"
            currentBuild.result = "FAILURE"
            def body = "${currentBuild.currentResult} ${env.JOB_NAME}${env.BUILD_DISPLAY_NAME}: on branch: ${params.GIT_BRANCH}. See details: ${env.BUILD_URL}"
            emailext body: body, recipientProviders: [[$class: 'DevelopersRecipientProvider'], [$class: 'CulpritsRecipientProvider'], [$class: 'RequesterRecipientProvider']], subject: "${currentBuild.currentResult}: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]'"
        }
    }
}

def execute(workPath, command, envs = '') {
    def buildpack = 'golang-buildpack:0.0.8'
    def repositoryName = 'kyma'
    def envText = envs=='' ? '' : "--env $envs"
    workDir = pwd()
    sh "docker run --rm -v $workDir:/go/src/github.com/kyma-project/$repositoryName/ -w /go/src/github.com/kyma-project/$repositoryName/$env.APP_FOLDER$workPath $envText ${env.DOCKER_REGISTRY}$buildpack /bin/bash -c '$command'"
}
